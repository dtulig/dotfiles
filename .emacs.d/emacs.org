#+title Emacs Configuration

* Basic Setup
** Small, global changes to enable first
After using company mode (completions) and lsp, I discovered major lag for lsp actions. Debugging indicated that emacs was doing a lot of garbage collection. Increasing this threshold resolves the issue at the cost of higher memory. I semi-arbitrarily picked 100mb.
#+begin_src emacs-lisp
  (setq gc-cons-threshold 100000000)
#+end_src

The initial pinentry setup causes issues when emacs is run in a terminal. Using loopback brings it into emacs.

#+begin_src emacs-lisp
  ;; Pinentry fixes popups in terminals for gpg
  (setq epa-pinentry-mode 'loopback)
#+end_src

Native comp should be silent, but still log.

#+begin_src emacs-lisp
  (setq native-comp-async-report-warnings-errors 'silent)
#+end_src

Define the current emacs configuration directory.

#+begin_src emacs-lisp
  (eval-when-compile
    (defun emacs-path (path)
	(expand-file-name path user-emacs-directory)))
#+end_src

Set where custom variables should be stored. This file isn't edited directly.

#+begin_src emacs-lisp
  (setq custom-file (emacs-path "custom.el"))
#+end_src

** Base for enabling emacs packages

This configures the package management system and sets use-package as the primary mechanism for managing packages going forward.

#+begin_src emacs-lisp
  (require 'package)

  (setq package-archives
		'(("gnu"         . "http://elpa.gnu.org/packages/")
		  ("nongnu"      . "https://elpa.nongnu.org/nongnu/")
		  ("melpa"       . "http://melpa.org/packages/")))

  (package-initialize)

  ;; Need to have emacs, or package, forget about built in org:
  ;; https://github.com/jwiegley/use-package/issues/319#issuecomment-845214233
  (assq-delete-all 'org package--builtins)
  (assq-delete-all 'org package--builtin-versions)

  (unless package-archive-contents
    (package-refresh-contents))


  (eval-when-compile
    (unless (package-installed-p 'use-package)
      (package-install 'use-package))

    (unless (package-installed-p 'delight)
      (package-install 'delight))

    (require 'use-package)
    (require 'delight))
#+end_src

** Mac OS X path setup
For Mac OS X, exec-path-from-shell is needed. This makes sure PATH is available for running programs from emacs. This needs to come early and before gpg so that the executable is found on the path.

#+begin_src emacs-lisp
  (use-package exec-path-from-shell
    :ensure t
    :demand t
    :config
    (when (memq window-system '(mac ns))
      (exec-path-from-shell-initialize)))
#+end_src

** Load encrypted information
#+begin_src emacs-lisp
  (org-babel-load-file (emacs-path "config.org.gpg"))
#+end_src
** Keep backup files in tmp
#+begin_src emacs-lisp
  ;; (setq backup-directory-alist `(("." . ,(expand-file-name
  ;; 					"/tmp/backups"))))

  (setq backup-directory-alist
	`((".*" . ,temporary-file-directory)))
  (setq auto-save-file-name-transforms
	`((".*" ,temporary-file-directory t)))

#+end_src

** Save a list of recent files visited
#+begin_src emacs-lisp
  (recentf-mode 1)
#+end_src
** Shorten the yes-no prompt to just y-n
#+begin_src emacs-lisp
  (defalias 'yes-or-no-p 'y-or-n-p)
#+end_src
** Spaces only
#+begin_src emacs-lisp
  (setq-default indent-tabs-mode nil)
#+end_src
** Remove trailing whitespace on save
#+begin_src emacs-lisp
  (add-hook 'before-save-hook 'delete-trailing-whitespace)
#+end_src

** Midnight configuration
#+begin_src emacs-lisp
  (use-package midnight
    :ensure t
    :demand t
    :init
    (midnight-delay-set 'midnight-delay "2:30am"))
#+end_src

** Electric Pair
#+begin_src emacs-lisp
  (use-package elec-pair
    :ensure t
    :demand t
    :config
    (electric-pair-mode 1))
#+end_src
* Basic UI Configuration
** Line Numbers
I want line numbers by default. A few modes will have the line numbers gutter disabled. Additionally, this turns on column numbers in the status bar.

#+begin_src emacs-lisp
  (column-number-mode)
  (global-display-line-numbers-mode)

  (dolist (mode '(org-mode-hook
		  term-mode-hook
		  shell-mode-hook
		  eshell-mode-hook))
    (add-hook mode (lambda () (display-line-numbers-mode 0))))
#+end_src
** Window Systems
*** All platforms
#+begin_src emacs-lisp
  (when window-system
    (setq frame-title-format '(buffer-file-name "%f" ("%b")))
    (blink-cursor-mode -1)
    (when (require 'mwheel nil 'no-error) (mouse-wheel-mode t)))

  (set-terminal-coding-system 'utf-8)
  (set-keyboard-coding-system 'utf-8)
  (prefer-coding-system 'utf-8)

  (setq visible-bell t
	echo-keystrokes 0.1
	font-lock-maximum-decoration t
	inhibit-startup-message t
	transient-mark-mode t
	color-theme-is-global t
	delete-by-moving-to-trash t
	shift-select-mode nil
	truncate-partial-width-windows nil
	uniquify-buffer-name-style 'forward
	whitespace-style '(trailing lines space-before-tab
				    indentation space-after-tab)
	whitespace-line-column 100
	ediff-window-setup-function 'ediff-setup-windows-plain
	;;oddmuse-directory (concat starter-kit-dir "oddmuse")
	xterm-mouse-mode t
	;;save-place-file (concat starter-kit-dir "places")
	)
#+end_src

*** Disable bell on mac os x
#+begin_src emacs-lisp
  (when (memq window-system '(mac ns))
    (setq visible-bell nil)
    (setq ring-bell-function (lambda ()
			       (invert-face 'mode-line)
			       (run-with-timer 0.1 nil 'invert-face 'mode-line))))
#+end_src

** Remove window dressings
#+begin_src emacs-lisp
  (when window-system
    (tooltip-mode -1)
    (tool-bar-mode -1)
    (scroll-bar-mode -1))

  (menu-bar-mode -1)
#+end_src
** Show matching parens when on point
#+begin_src emacs-lisp
  (show-paren-mode 1)
#+end_src

** Font faces for todo, etc
In programming modes, TODO and a few other keywords are useful for making notes. This sets up faces and styles for those keywords in the specified fixme modes.
#+begin_src emacs-lisp
  (setq fixme-modes '(rust-mode
                      c++-mode
                      c-mode
                      emacs-lisp-mode
                      rustic-mode
                      python-mode python-ts-mode
                      terraform-mode))
  (make-face 'font-lock-fixme-face)
  (make-face 'font-lock-study-face)
  (make-face 'font-lock-important-face)
  (make-face 'font-lock-note-face)
  (mapc (lambda (mode)
          (font-lock-add-keywords
           mode
           '(("\\<\\(TODO\\)" 1 'font-lock-fixme-face t)
             ("\\<\\(STUDY\\)" 1 'font-lock-study-face t)
             ("\\<\\(IMPORTANT\\)" 1 'font-lock-important-face t)
             ("\\<\\(NOTE\\)" 1 'font-lock-note-face t))))
        fixme-modes)
  (modify-face 'font-lock-fixme-face "red" nil nil t nil t nil nil)
  (modify-face 'font-lock-study-face "orange" nil nil t nil t nil nil)
  (modify-face 'font-lock-important-face "orange" nil nil t nil t nil nil)
  (modify-face 'font-lock-note-face "forest green" nil nil t nil t nil nil)
#+end_src

* Keybinding Configuration
This configuration overhauls the keybindings with evil-mode. Two important notes:
 - This configuration sets C-u for vim-like scrolling. I might need to re-bind C-u.
 - The evil-collection package configures a variety of Emacs modes with better Vi-like bindings.

#+begin_src emacs-lisp
  (global-set-key (kbd "C-M-h") 'backward-kill-word)
  ; use ibuffer instead of list-buffers
  (global-set-key (kbd "C-x C-b") 'ibuffer)

  (use-package evil
    :ensure t
    :demand t
    :init
    (setq evil-want-C-i-jump nil)
    (setq evil-esc-delay 0)
    (setq evil-want-C-u-scroll t)
    (setq evil-want-keybinding nil)
    :config
    (evil-global-set-key 'motion "j" 'evil-next-visual-line)
    (evil-global-set-key 'motion "k" 'evil-previous-visual-line)
    (evil-set-initial-state 'rustic-popup-mode 'emacs)
    (evil-mode 1))

  (use-package evil-surround
    :after evil
    :ensure t
    :demand t
    :config
    (global-evil-surround-mode 1))

  (use-package evil-collection
    :after evil
    :ensure t
    :config
    (evil-collection-init 'dired)
    (evil-collection-init 'w3m)
    (evil-collection-init 'imenu)
    (evil-collection-init 'sly)
    (evil-collection-init 'imenu-list)
    (evil-collection-init 'xref)
    (evil-collection-init 'ibuffer))

#+end_src
* Expanded UI Configuration
** Base16 theming
Base16 has themes on a variety of platforms. This works best when paired with the base16 shell functionality (urxvt, iTerm2).

I'm overriding the header-line face, which is used in the list mode as a header. A good tip is to run list-faces-display, which shows all faces. Describing faces is expanded in this [[https://emacs.stackexchange.com/questions/303/describe-face-character-not-under-unreachable-by-the-cursor][stackexchange answer]].

#+begin_src emacs-lisp
  (use-package base16-theme
    :ensure t
    :demand t
    :config
    (setq base16-highlight-mode-line 'contrast)
    (setq base16-theme-256-color-source 'base16-shell)
    (load-theme 'base16-nord t)
    (custom-set-faces
     '(header-line ((t (:foreground "color-18" :inherit highlight)))))
    (set-variable 'frame-background-mode 'dark))
#+end_src

** imenu Improvements
#+begin_src emacs-lisp
  (use-package imenu-list
    :ensure t
    :demand t
    ;:bind ("C-c l" . imenu-list-smart-toggle)
    )
#+end_src
** Improving the mode line
#+begin_src emacs-lisp
  (use-package smart-mode-line
    :ensure t
    :demand t
    :config
    (setq sml/no-confirm-load-theme t)
    (setq sml/theme 'respectful)
    (sml/setup))
#+end_src
** Which Key
This is a great package that shows all completions as key chords are entered.
#+begin_src emacs-lisp
  (use-package which-key
    :ensure t
    :init (which-key-mode)
    :delight
    :config
    (setq which-key-idle-delay 1.0))
#+end_src

** Ivy and Counsel
#+begin_src emacs-lisp
  (use-package marginalia
    :ensure t
    :config
    (marginalia-mode))

  (use-package vertico
    :ensure t
    :delight
    :demand t
    :init
    (vertico-mode))

  (use-package consult
    :ensure t
    :demand t
    :delight
    :bind (("C-c i" . consult-imenu)
           ("C-s" . consult-line)
           ("C-c j" . consult-git-grep)
           ("C-c k" . consult-ripgrep)
           ("C-c o" . consult-yank-pop)))


  (use-package orderless
    :ensure t
    :custom
    (completion-styles '(orderless basic))
    (completion-category-overrides '((file (styles basic partial-completion)))))

  (use-package embark
    :ensure t

    :bind
    (("C-." . embark-act)
     ("C-c C-o" . embark-dwim)
     ("C-h B" . embark-bindings))

    :init

    (setq prefix-help-command #'embark-prefix-help-command)

    ;; Show the Embark target at point via Eldoc. You may adjust the
    ;; Eldoc strategy, if you want to see the documentation from
    ;; multiple providers. Beware that using this can be a little
    ;; jarring since the message shown in the minibuffer can be more
    ;; than one line, causing the modeline to move up and down:

    ;; (add-hook 'eldoc-documentation-functions #'embark-eldoc-first-target)
    ;; (setq eldoc-documentation-strategy #'eldoc-documentation-compose-eagerly)

    :config

    (add-to-list 'display-buffer-alist
                 '("\\`\\*Embark Collect \\(Live\\|Completions\\)\\*"
                   nil
                   (window-parameters (mode-line-format . none)))))

  (use-package embark-consult
    :ensure t
    :demand t
    :hook
    (embark-collect-mode . consult-preview-at-point-mode))
#+end_src
* LLM
#+begin_src emacs-lisp
  (use-package gptel
    :ensure t
    :config
    (setq
     gptel-model 'mistral-nemo
     gptel-backend (gptel-make-ollama "Ollama"
                     :host "localhost:11434"
                     :stream t
                     :models '("qwen2.5-coder:7b" "mistral-nemo"))))
#+end_src
* Org Mode
** General Settings
#+begin_src emacs-lisp
  (setq org-directory (expand-file-name "~/Sync/org/"))
  (setq org-log-done 'time)
  (setq org-default-notes-file (concat org-directory "/notes.org"))
  (setq org-refile-use-outline-path 'nil)
  (setq org-agenda-clockreport-parameter-plist
        (quote (:link t :fileskip0 t)))
  (setq org-clock-in-resume t)
  (setq org-drawers (quote ("PROPERTIES" "LOGBOOK")))
  (setq org-clock-into-drawer t)
  (setq org-clock-out-remove-zero-time-clocks t)
  (setq org-clock-out-when-done t)
  (setq org-refile-targets (quote ((nil :maxlevel . 3)
        			   (org-agenda-files :maxlevel . 3))))
  ;; Allows text under org headlines to start at the far left
  (setq org-adapt-indentation nil)
  (setq org-default-priority ?C)
  (setq org-id-link-to-org-use-id t)
#+end_src
** Todo Keywords and States Configuration
#+begin_src emacs-lisp
  (setq org-todo-keywords
	(quote ((sequence "TODO(t)" "NEXT(n)" "|" "DONE(d)")
		(sequence "WAITING(w@/!)" "DELEGATED(e@/!)" "HOLD(h@/!)" "|" "CANCELLED(c@/!)" "PHONE" "MEETING"))))

  (setq org-todo-keyword-faces
	(quote (("TODO" :foreground "red" :weight bold)
		("NEXT" :foreground "#268bd2" :weight bold)
		("DONE" :foreground "forest green" :weight bold)
		("WAITING" :foreground "orange" :weight bold)
		("DELEGATED" :foreground "orange" :weight bold)
		("HOLD" :foreground "magenta" :weight bold)
		("CANCELLED" :foreground "forest green" :weight bold)
		("MEETING" :foreground "forest green" :weight bold)
		("PHONE" :foreground "forest green" :weight bold))))

  (setq org-todo-state-tags-triggers
	(quote (("CANCELLED" ("CANCELLED" . t))
		("WAITING" ("WAITING" . t))
		("DELEGATED" ("DELEGATED" . t))
		("HOLD" ("WAITING") ("DELEGATED") ("HOLD" . t))
		(done ("WAITING") ("DELEGATED") ("HOLD"))
		("TODO" ("WAITING") ("DELEGATED") ("CANCELLED") ("HOLD"))
		("NEXT" ("WAITING") ("DELEGATED") ("CANCELLED") ("HOLD"))
		("DONE" ("WAITING") ("DELEGATED") ("CANCELLED") ("HOLD")))))

#+end_src
** Project customizations
#+begin_src emacs-lisp
  (defvar org-projects-dir (expand-file-name  (concat org-directory "projects/")))

  (defun gf/create-org-path (path)
    "Create a name suitable for an org file from the last part of a file
    path."
    (let ((last (car (last (split-string (if (equal (substring path -1) "/")
					     (substring path 0 -1) path) "/")))))
      (concat org-projects-dir "/"
	      (downcase
	       (replace-regexp-in-string
		"\\." "-" (if (equal (substring last 0 1) ".")
			      (substring last 1) last)))
	      ".org")))

  (defun gf/project-org-file ()
    "Get the path of the org file for the current project."
    (gf/create-org-path (projectile-project-root)))

  (defun gf/switch-to-project-org-file ()
    "Switch to the org file for the current project."
    (interactive)
    (find-file (gf/project-org-file)))
#+end_src

** Reload function
#+begin_src emacs-lisp
  (defun dt/org-reload ()
    (interactive)
    (setq org-agenda-files (append (file-expand-wildcards (concat org-directory "dates/*.org"))
				   (file-expand-wildcards (concat org-directory "*.org"))
				   (file-expand-wildcards (concat org-directory "projects/*.org"))
				   (file-expand-wildcards (concat org-directory "steadily/dates/*.org"))
				   (file-expand-wildcards (concat org-directory "steadily/*.org"))
				   (file-expand-wildcards (concat org-directory "indeed/dates/*.org"))
				   (file-expand-wildcards (concat org-directory "indeed/*.org"))))
    (setq org-default-personal-notes-file
	  (concat org-directory "dates/"
		  (downcase (format-time-string "%Y-%B-p.org"))))
    (setq org-default-work-notes-file
	  (concat org-directory "indeed/dates/"
		  (downcase (format-time-string "%Y-%B-w.org"))))
    (setq org-default-steadily-notes-file
	  (concat org-directory "steadily/dates/"
		  (downcase (format-time-string "%Y-%B-w.org"))))
    (setq org-default-journal-file
	  (concat org-directory "journal.org")))

#+end_src
** Capture templates
#+begin_src emacs-lisp
  (setq org-capture-templates
	'(("t" "Tasks")
	  ("ts" "Steadily" entry (file+headline
				    org-default-steadily-notes-file "Tasks")
	   "* TODO %^{Description}
  %U
  %?
  " :clock-in t :clock-resume t :empty-lines 1)
	  ("tp" "Personal" entry (file+headline
				    org-default-personal-notes-file "Tasks")
	   "* TODO %^{Description}
  %U
  %?
  " :clock-in t :clock-resume t)
	  ;; Create a meeting entry
	  ("m" "Meeting")
	  ("ms" "Steadily" entry (file+headline
				org-default-steadily-notes-file "Meetings")
	   "* MEETING with %^{Description} :MEETING:
  %U
  %?" :clock-in t :clock-resume t)
	  ("mp" "Personal" entry (file+headline
				org-default-personal-notes-file "Meetings")
	   "* MEETING with %^{Description} :MEETING:
  %U
  %?" :clock-in t :clock-resume t)
	  ;; Create a todo with mu4e link
	  ("r" "Respond")
	  ("rs" "Steadily" entry (file+headline
				       org-default-steadily-notes-file "Tasks")

	   "* NEXT Respond to %:from on %:subject
  SCHEDULED: %t
  %U
  %a
  " :clock-in t :clock-resume t :immediate-finish t)
	  ("rp" "Personal" entry (file+headline
					   org-default-personal-notes-file "Tasks")
	   "* NEXT Respond to %:from on %:subject
  SCHEDULED: %t
  %U
  %a
  " :clock-in t :clock-resume t :immediate-finish t)
	  ("n" "Note")
	  ("ns" "Steadily" entry (file+headline
			     org-default-steadily-notes-file "Notes")
	   "* %? :NOTE:
  %U
  " :clock-in t :clock-resume t)
	  ("np" "Personal" entry (file+headline
				  org-default-personal-notes-file "Notes")
	   "* %? :NOTE:
  %U
  " :clock-in t :clock-resume t)
	  ("P" "Todo (Project)" entry (file+headline
				       gf/project-org-file "Tasks")
	   "* TODO %^{Description}
  %U
  %?
  " :clock-in t :clock-resume t)
	  ("N" "Project Note" entry (file+headline
				     gf/project-org-file "Notes")
	   "* %?
  %U
  ")
	  ("j" "Journal" entry (file+olp+datetree org-default-journal-file)
	   "* %^{Title}
  %U
  %?
  " :clock-in t :clock-resume t)
	  ("l" "Log Time" entry (file+olp+datetree
				 (concat org-directory "/timelog.org"))
	   "** %U - %^{Activity}  :TIME:")))
#+end_src
** Agenda
#+begin_src emacs-lisp
  (setq org-agenda-span 'day)

  (require 'cl-lib)

  (defun buffer-major-mode-org-mode-p (buffer)
    (string= "org-mode" (with-current-buffer buffer major-mode)))

  ;; When refreshing the org mode window, occasionally a file will have
  ;; shifted underneath the current instance of emacs. This function
  ;; will close all org-mode buffers.
  (defun org-close-all-org-buffers ()
    (interactive)
    (mapcar #'kill-buffer
            (cl-remove-if-not #'buffer-major-mode-org-mode-p (buffer-list))))

  ;; This is a global key to close all org mode buffers.
                                          ;(global-set-key "\C-c\C-g" 'org-close-all-org-buffers)

  (defun org-agenda-redo-with-close-buffers ()
    (interactive)
    (org-close-all-org-buffers)
    (org-agenda-redo t))

  (defun dt/org-ql-project-todo-excludes ()
    '(and (todo)
          (not (todo "SOMEDAY"))
          (not (todo "CANCELLED"))
          (not (todo "DELEGATED"))
          (not (todo "DONE"))
          (not (tags "SOMEDAY"))
          (not (tags "HOLD"))))

  ;; I liked http://doc.norang.ca/org-mode.html#WhatDoIWorkOnNext,
  ;; taking some of that.
  (setq org-agenda-custom-commands
        (quote (("A" "Agenda"
                 ((agenda "" nil)
                  (org-ql-block '(and (or (priority "A") (priority "B"))
                                      (not (or (todo "DONE") (todo "CANCELLED"))))
                                ((org-ql-block-header "High-priority unfinished tasks")))
                  (org-ql-block (append (dt/org-ql-project-todo-excludes)
                                        '((and (descendants
                                                (or (todo)
                                                    (todo "CANCELLED")
                                                    (todo "DONE")))
                                               (not (descendants
                                                     (todo "NEXT"))))))
                                ((org-ql-block-header "Stuck Projects")))
                  (todo "DELEGATED|WAITING"
                             ((org-agenda-overriding-header "Delegated / Waiting")
                              (org-agenda-sorting-strategy
                               '(priority-down category-keep))))
                  (org-ql-block (append (dt/org-ql-project-todo-excludes)
                                        '((and (descendants
                                                (todo "NEXT")))))
                                ((org-ql-block-header "Projects")))
                  (org-ql-block '(and (todo "NEXT")
                                      (ancestors (and (todo "TODO")
                                                      (not (tags "SOMEDAY")))))
                                ((org-ql-block-header "Project Next Tasks")))
                  (org-ql-block '(and (or (todo "TODO")
                                          (todo "DELEGATED"))
                                      (ancestors (and (todo "TODO")
                                                      (not (tags "SOMEDAY")))))
                                ((org-ql-block-header "Project Subtasks")))
                  (org-ql-block '(and (todo "NEXT")
                                      (not (tags "SOMEDAY"))
                                      (not (children))
                                      (not (ancestors (todo))))
                                ((org-ql-block-header "Single Tasks"))))
                 nil))))

  (defun dt/org-ql-view--format-element (orig-fun &rest args)
    "This function will intercept the original function and
  add the category to the result.

  ARGS is `element' in `org-ql-view--format-element'"
    (if (not args)
        ""
      (let* ((element args)
             (properties (cadar element))
             (result (apply orig-fun element))
             (smt "")
             (category (org-entry-get (plist-get properties :org-marker) "CATEGORY")))
        (if (> (length category) 15)
            (setq category (substring category 0 14)))
        (if (< (length category) 15)
            (setq smt (make-string (- 15 (length category)) ?\s)))
        (org-add-props
         (format "  %-8s %s" (concat category ":" smt) result)
            (text-properties-at 0 result)))))

  (advice-add 'org-ql-view--format-element :around #'dt/org-ql-view--format-element)

#+end_src
** Use package declaration
#+begin_src emacs-lisp
  (defun dt/org-mode-setup ()
    (visual-line-mode 1))

  (use-package org
    :ensure t
    :pin gnu)

  (use-package org-ql
    :ensure t
    :after org)

  (use-package ob-http
    :ensure t)

  (use-package org-contrib
    :ensure t
    :after org midnight ob-http
    :pin nongnu
    :demand t
    :bind (("C-c s" . org-store-link)
           ("C-c c" . org-capture)
           ("C-c a" . org-agenda))
    :hook (;; (org-agenda-mode . (lambda ()
           ;; 		     (add-hook 'auto-save-hook 'org-save-all-org-buffers nil t)
           ;; 		     (auto-save-mode)))
           (org-mode . dt/org-mode-setup)
           (midnight-mode . dt/org-reload))
    :config
    (require 'org-checklist)
    (dt/org-reload)
    (org-babel-do-load-languages
     'org-babel-load-languages
     '((emacs-lisp . t)
       (python . t)
       (shell . t)
       (http . t))))

  ;; https://github.com/Somelauw/evil-org-mode
  (use-package evil-org
    :ensure t
    :after org evil
    :hook (org-mode . (lambda () evil-org-mode))
    :config)

  (use-package evil-org-agenda
    :ensure nil
    :after evil-org
    :config
    (evil-org-agenda-set-keys)
    (evil-define-key 'motion org-agenda-mode-map
      "so" 'org-save-all-org-buffers
      "gl" 'org-agenda-redo-with-close-buffers))

  ;; Find ivy equivalent
  ;;(global-set-key "\C-cb" 'org-iswitchb)

  (advice-add 'org-refile :after 'org-save-all-org-buffers)

  (add-to-list 'org-structure-template-alist '("el" . "src emacs-lisp"))
  (add-to-list 'org-structure-template-alist '("sh" . "src sh"))
#+END_SRC
** Org habits
#+begin_src emacs-lisp
  (use-package org-habit
    :after org-contrib
    :ensure nil
    :config
    (add-to-list 'org-modules 'org-habit t)
    (setq
     ;; Position the habit graph to the right.
     org-habit-graph-column 60)
    (setq org-habit-show-all-today t))
#+end_src
* Development
** IDE Features with lsp-mode
#+begin_src emacs-lisp
  (use-package eglot
    :ensure t
    :hook ((rustic-mode . eglot-ensure)
           (python-mode . dt/python-eglot-mode-setup)
           (python-ts-mode . dt/python-eglot-mode-setup))
    :bind (:map eglot-mode-map
                ("C-c l a a" . eglot-code-actions)
                ("C-c l r" . eglot-rename)
                ("C-c l f" . eglot-format-buffer))
    ;; :init
    ;; (setq eglot-ignored-server-capabilities '(:documentHighlightProvider))
    :config
    (setq eglot-workspace-configuration
          '((:rust-analyzer . (:cargo (:allTargets t)
                                      :check (:allFeatures t)))
            ;; (:pylsp . (:configurationSources ["flake8" "black"]
            ;;            :plugins (:pycodestyle (:enabled :json-false)
            ;;                                   :mccabe (:enabled :json-false)
            ;;                                   :pyflakes (:enabled :json-false)
            ;;                                   :flake8 (:enabled t)
            ;;                                   :pydocstyle (:enabled :json-false) ; reconsider
            ;;                                   :yapf (:enabled :json-false)
            ;;                                   :autopep8 (:enabled :json-false)
            ;;                                   :black (:enabled t)
            ;;                                   :rope (:enabled t)))))
          )))
#+end_src
** Company Mode for completions
#+begin_src emacs-lisp
  (use-package company
    :ensure t
    :demand t
    :bind (("C-<tab>" . counsel-company))
    ;; (:map company-active-map
    ;; 	("<tab>" . company-complete-selection))
    :custom
    (company-tooltip-align-annotations t)
    (company-tooltip-flip-when-above t)
    (company-require-match nil)
    (company-minimum-prefix-length 1)
    (company-show-numbers t)
    ;(company-idle-delay nil)
    ;(company-format-margin-function #'company-vscode-dark-icons-margin)
    (company-occurrence-weight-function #'company-occurrence-prefer-any-closest)
    (company-dabbrev-minimum-length 2)
    (company-dabbrev-code-modes t)
    (company-dabbrev-code-everywhere t)
    ;; (company-backends '((company-capf company-yasnippet company-dabbrev-code)))
    :config (global-company-mode 1))

#+end_src
** Magit
#+begin_src emacs-lisp
  (use-package magit
    :ensure t
    :bind (("C-x g" . magit-status)
	   ("C-x M-g" . magit-dispatch))
    :config
    (add-hook 'git-commit-setup-hook 'turn-off-auto-fill t))

  (use-package diff-hl
    :ensure t
    :init (global-diff-hl-mode))
#+end_src
** Project Management via Project el
#+begin_src emacs-lisp
  (use-package project
    :ensure t
    :bind-keymap ("C-c p" . project-prefix-map)
    :config
    (setq project-vc-extra-root-markers' (".project")))

  (use-package projectile
    :after counsel
    :ensure t
    :bind-keymap ("C-c r" . projectile-command-map)
    :init
    (when (file-directory-p "~/workspace")
      (setq projectile-project-search-path '("~/workspace")))
    :config
    (setq projectile-mode-line
          '(:eval (format " Projectile[%s]"
        		  (projectile-project-name))))
    (setq projectile-completion-system 'counsel)
    (setq projectile-switch-project-action 'counsel-projectile)
    (projectile-mode +1))
#+end_src
** Flycheck
#+begin_src emacs-lisp
  (use-package flymake
    :ensure t
    :bind (:map flymake-mode-map
                ("M-n" . flymake-goto-next-error)
                ("M-p" . flymake-goto-prev-error)))
#+end_src
** Language Support
#+begin_src emacs-lisp
  ;; (use-package paredit
  ;;   :ensure t
  ;;   :hook ((emacs-lisp-mode . paredit-mode)
  ;;          (lisp-mode . paredit-mode)))

  (use-package rainbow-delimiters
    :ensure t
    :hook (prog-mode . rainbow-delimiters-mode))

  (use-package just-mode
    :ensure t
    :hook (just-mode . (lambda () (setq tab-width 2))))
#+end_src
*** Rust
#+begin_src emacs-lisp
  ;; This installed markdown mode, projectile
  (use-package rustic
    :after projectile
    :ensure t
    :hook ((rustic-mode . electric-pair-mode)
           (rustic-mode . auto-revert-mode))
    :custom (rustic-lsp-client 'eglot))

#+end_src
*** Python

Install python-lsp-server.

#+begin_src emacs-lisp
  ;; (defun dt/python-lsp-mode-setup ()
  ;;   (when (stringp (poetry-find-project-root))
  ;;     (poetry-venv-workon))
  ;;   (require 'lsp-pyright)
  ;;   (lsp-deferred))

  ;; (use-package lsp-pyright
  ;;   :ensure t
  ;;   :hook (python-mode . dt/python-lsp-mode-setup))

  (defun dt/python-eglot-mode-setup ()
    (when (stringp (poetry-find-project-root))
      (poetry-venv-workon))
    (require 'eglot)
    (eglot-ensure))

  (add-to-list 'major-mode-remap-alist '(python-mode . python-ts-mode))

  (use-package poetry
    :ensure t)
#+end_src
*** GLSL (graphic shading language)
#+begin_src emacs-lisp
  (use-package glsl-mode
    :ensure t
    :mode (("\\.glsl\\'" . glsl-mode)
	   ("\\.vert\\'" . glsl-mode)
	   ("\\.frag\\'" . glsl-mode)
	   ("\\.geom\\'" . glsl-mode)))

#+end_src
*** Yaml
#+begin_src emacs-lisp
  (use-package yaml-mode
    :ensure t)
#+end_src
*** JavaScript/Typescript
#+begin_src emacs-lisp
  ;; (use-package js2-minor-mode
  ;;   :ensure t)
  (setq js-indent-level 2)

  (use-package typescript-mode
    :ensure t
    :hook (typescript-mode . eglot-ensure)
    :custom (typescript-indent-level 2))
#+end_src
*** Web
#+begin_src emacs-lisp
  (use-package web-mode
    :ensure t
    :mode (("\\.liquid\\'" . web-mode)
           ("\\.jinja2\\'". web-mode)
           ("\\.j2\\'". web-mode)
           ("\\.html\\'". web-mode)
           ("\\.tsx\\'" . typescript-tsx-mode))
    :hook
    ((typescript-tsx-mode . eglot-ensure))
    :init
    (define-derived-mode typescript-tsx-mode web-mode "TypeScript-tsx")
    :custom
    (setq web-mode-engines-alist '(("django" . "\\.html\\'")))
    (web-mode-markup-indent-offset 2)
    (web-mode-css-indent-offset 2)
    (web-mode-code-indent-offset 2)
    (web-mode-script-padding 2)
    (web-mode-style-padding 2))
#+end_src
*** Common Lisp
#+begin_src emacs-lisp
  (use-package sly
    :ensure t
    :config
    (setq sly-lisp-implementations
          '((sbcl ("/usr/bin/sbcl" "--dynamic-space-size" "8192")))))

  (use-package lispy
    :ensure t)

  (use-package lispyville
    :ensure t
    :hook ((emacs-lisp-mode . lispyville-mode)
           (lisp-mode . lispyville-mode))
    :config
    (lispyville-set-key-theme '(operators c-w
                                          (escape insert)
                                          (additional-movement normal visual motion)
                                          (slurp/barf-cp normal))))
#+end_src
*** C / C++
#+begin_src emacs-lisp
  (setq-default c-basic-offset 4)
  (setq-default c-default-style "k&r")
#+end_src

*** Terraform
#+begin_src emacs-lisp
  (use-package terraform-mode
    :ensure t
    :custom (terraform-format-on-save-mode t)
    :hook (terraform-mode-hook . dt/terraform-mode-init)
    :config
    (defun dt/terraform-mode-init ()
      (outline-minor-mode 1)))
#+end_src

*** Haskell

#+begin_src emacs-lisp
  (use-package haskell-mode
    :ensure t)

  ;; (use-package haskell-mode
  ;;   :bind (:map haskell-mode-map
  ;;               (("C-c C-c" . haskell-compile))))

  ;; ((nil . ((eglot-workspace-configuration . (:haskell
  ;;                                            (:plugins
  ;;                                             (:stan (:globalOn :json-false)
  ;;                                                    :ghcide-code-actions-fill-holes (:enabled t))))))))
#+end_src

*** Zig

#+begin_src emacs-lisp
  (use-package zig-mode
    :ensure t)
#+end_src

* Writing
#+begin_src emacs-lisp
  (use-package flyspell
    :hook ((text-mode . flyspell-mode)
           (prog-mode . flyspell-prog-mode))
    ;; :custom (flyspell-auto-correct-binding "C-x C-j")
    )

  (use-package writegood-mode
    :ensure t)
#+end_src

** Visual Fill Mode
#+begin_src emacs-lisp
  (defun dt/org-mode-visual-fill ()
    (setq visual-fill-column-width 100
	  visual-fill-column-center-text nil)
    (visual-fill-column-mode 1))

  (use-package visual-fill-column
    :ensure t
    :hook (org-mode . dt/org-mode-visual-fill))
#+end_src
* Yasnippet

Some snippets from the yasnippet-snippets package depend on hooks. I want to install the snippets manually but leverage that shared package. A few methods are necessary to make this work well.

#+begin_src emacs-lisp
  (defun yasnippet-snippets--fixed-indent ()
    "Set `yas-indent-line' to `fixed'."
    (set (make-local-variable 'yas-indent-line) 'fixed))

  (defun yasnippet-snippets--no-indent ()
    "Set `yas-indent-line' to nil."
    (set (make-local-variable 'yas-indent-line) nil))
#+end_src

#+begin_src emacs-lisp
  (use-package yasnippet
    :ensure t
    :bind (("C-c y d" . yas-load-directory)
           ("C-c y i" . yas-insert-snippet)
           ("C-c y f" . yas-visit-snippet-file)
           ("C-c y n" . yas-new-snippet)
           ("C-c y t" . yas-tryout-snippet)
           ("C-c y l" . yas-describe-tables)
           ("C-c y g" . yas/global-mode)
           ("C-c y m" . yas/minor-mode)
           ("C-c y r" . yas-reload-all)
           ("C-c y x" . yas-expand))
    :config
    (when dt/config-yas-snippets-dir
      (add-to-list 'yas-snippet-dirs dt/config-yas-snippets-dir t))
    (yas-global-mode 1))

  (use-package yasnippet-snippets
    :ensure t)

#+end_src
* Email and Mu4e
** Check if this machine is setup for mu4e
#+begin_src emacs-lisp
  (setq dt-mu4e-path (let ((possible-paths '("/var/run/current-system/sw/share/emacs/site-lisp/mu4e"
					     "/usr/local/share/emacs/site-lisp/mu4e"
					     "/usr/local/share/emacs/site-lisp/mu/mu4e"
					     "/usr/share/emacs/site-lisp/mu")))
		       (seq-find #'file-exists-p possible-paths)))

#+end_src
** Configure mu4e
#+begin_src emacs-lisp
  (when dt-mu4e-path
    (use-package mu4e
      :after org-contrib
      :load-path dt-mu4e-path
      :config
      (require 'org-mu4e)

      (setq mu4e-maildir "~/.mail/gmail")
      (setq mu4e-drafts-folder "/[Gmail].Drafts")
      (setq mu4e-sent-folder   "/[Gmail].Sent Mail")
      (setq mu4e-trash-folder  "/[Gmail].Trash")
      (setq mu4e-refile-folder "/ImapArchive")

      (setq mu4e-headers-sort-direction 'ascending)

      (require 'mu4e-contrib)
      (setq mu4e-html2text-command 'mu4e-shr2text)
      (setq mu4e-change-filenames-when-moving t)
      (setq mu4e-sent-messages-behavior 'delete)

      (add-hook 'mu4e-compose-mode-hook 'turn-off-auto-fill)

      (setq mu4e-maildir-shortcuts
	    '( ("/INBOX"               . ?i)
	       ("/[Gmail].Sent Mail"   . ?s)
	       ("/[Gmail].Trash"       . ?t)
	       ("/[Gmail].All Mail"    . ?a)))

      (fset 'my-move-to-trash "mt")
      (define-key mu4e-headers-mode-map (kbd "d") 'my-move-to-trash)
      (define-key mu4e-view-mode-map (kbd "d") 'my-move-to-trash)

      (fset 'my-archive "D")
      (define-key mu4e-headers-mode-map (kbd "e") 'my-archive)
      (define-key mu4e-view-mode-map (kbd "e") 'my-archive)

      (setq mu4e-get-mail-command "mbsync -a")
      (setq mu4e-bookmarks
	    '(
	      ("(flag:unread AND NOT (flag:trashed OR maildir:\"/[Gmail].Trash\") AND NOT ((maildir:\"/[Gmail].Spam\") OR (maildir:\"/[Gmail].All Mail\") OR (maildir:\"/[Gmail].Important\")) OR maildir:\"/[Gmail].Inbox\""
	       "Daily Review" ?d)
	      ("flag:unread AND NOT (flag:trashed OR maildir:\"/[Gmail].Trash\") AND NOT list:* AND NOT maildir:\"/[Gmail].Spam\""
	       "Unread messages, no lists" ?U)
	      ("flag:unread AND NOT (flag:trashed OR maildir:\"/[Gmail].Trash\") AND NOT maildir:\"/[Gmail].Spam\""
	       "All unread messages" ?u)
	      ("flag:unread AND list:* AND NOT maildir:\"/[Gmail].Spam\" AND NOT maildir:\"/[Gmail].Trash\""
	       "Unread lists" ?l)
	      ("date:today..now"                  "Today's messages"     ?t)
	      ("date:7d..now"                     "Last 7 days"          ?w)
	      ("mime:image/*"                     "Messages with images" ?p)))

      (require 'smtpmail)

      (defun read-lines (filePath)
	"Return a list of lines of a file at filePath."
	(with-temp-buffer
	  (insert-file-contents filePath)
	  (split-string (buffer-string) "\n" t)))

      (defun get-string-from-file (filePath)
	"Return filePath's file content."
	(with-temp-buffer
	  (insert-file-contents filePath)
	  (buffer-string)))

      (setq message-send-mail-function 'smtpmail-send-it
	    smtpmail-stream-type 'starttls
	    smtpmail-default-smtp-server "smtp.gmail.com"
	    smtpmail-smtp-server "smtp.gmail.com"
	    smtpmail-smtp-service 587)

      (setq message-kill-buffer-on-exit t)

      (defun org-mu4e-store-link ()
	"Store a link to a mu4e query or message."
	(cond
	 ;; storing links to queries
	 ((eq major-mode 'mu4e-headers-mode)
	  (let* ((query (mu4e-last-query))
		 desc link)
	    (org-store-link-props :type "mu4e" :query query)
	    (setq
	     desc (concat "mu4e:query:" query)
	     link desc)
	    (org-add-link-props :link link :description desc)
	    link))
	 ;; storing links to messages
	 ((eq major-mode 'mu4e-view-mode)
	  (let* ((msg  (mu4e-message-at-point))
		 (msgid   (or (plist-get msg :message-id) "<none>"))
		 (from (car (car (mu4e-message-field msg :from))))
		 (to (car (car (mu4e-message-field msg :to))))
		 (subject (mu4e-message-field msg :subject))
		 link)
	    (setq link (concat "mu4e:msgid:" msgid))
	    (org-store-link-props :type "mu4e" :link link
				  :message-id msgid)
	    (setq link (concat "mu4e:msgid:" msgid))
	    (org-store-link-props
	     :type "mu4e" :from from :to to :subject subject
	     :message-id msgid)

	    (org-add-link-props :link link
				:description (funcall org-mu4e-link-desc-func msg))
	    link))))

      (org-add-link-type "mu4e" 'org-mu4e-open)
      (add-hook 'org-store-link-functions 'org-mu4e-store-link)

      (add-to-list 'mu4e-view-actions '("rview related" . djr/mu4e-view-related-search) t)
      (defun djr/mu4e-view-related-search (msg)
	"Search for related messages to the current one"
	(let* ((msgid (mu4e-msg-field msg :message-id)))
	  (setq mu4e-headers-include-related t)
	  (mu4e-headers-search (concat "msgid:" msgid))))))
#+end_src
* W3m
#+begin_src emacs-lisp
  (use-package w3m
    :ensure t
    :bind (("C-c w s" . w3m-search)
	   ("C-c w S" . w3m-search-new-session)
	   ("C-c w o" . w3m-browse-url)
	   ("C-c w m" . dt/w3m-mpv)
	   ("C-c w f" . dt/w3m-open-current-page-in-firefox)
	   ("C-c w F" . dt/w3m-open-link-or-image-in-firefox))
    :config
    (setq w3m-search-default-engine "duckduckgo")
    (setq w3m-use-title-buffer-name t))

  (defun dt/w3m-mpv ()
    (interactive)
    (let ((link (w3m-anchor)))
      (if (not link)
	  (message "The point is not a link.")
	(cond ((string-match "/\\(www\\|m\\)\\.youtube\\.com\\/watch\/?" link)
	       (message (concat "Loading from youtube: " link))
	       (start-process "mpv" nil "mpv" link))
	      ((string-match "/youtu\\.be\\//?" link)
	       (message (concat "Loading from youtube: " link))
	       (start-process "mpv" nil "mpv" link))))))

  (defun dt/w3m-open-current-page-in-firefox ()
    "Open the current URL in Mozilla Firefox."
    (interactive)
    (browse-url-firefox w3m-current-url))

  (defun dt/w3m-open-link-or-image-in-firefox ()
    "Open the current link or image in Firefox."
    (interactive)
    (browse-url-firefox (or (w3m-anchor)
			    (w3m-image))))
#+end_src
* Emacs Server
#+begin_src emacs-lisp
  (require 'server)
  (unless (server-running-p)
    (server-start))
#+end_src
